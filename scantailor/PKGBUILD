# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Denis Terskov aka neurosurgeon <terskov.den@gmail.com>

pkgname=scantailor
pkgver=0.9.12.2
pkgrel=3
_realver=EXPERIMENTAL_2015_10_27
pkgdesc="Interactive post-processing tool for scanned pages"
arch=(i686 x86_64)
url="http://scantailor.org/"
license=("GPL")
depends=('qt5-base' 'libtiff' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('cmake' 'qt5-tools' 'eigen' 'boost')
source=($pkgname-$_realver.tar.gz::https://github.com/Tulon/$pkgname/archive/$_realver.tar.gz
        scantailor.desktop
        zero-button.patch
        qbutton-group-inc.patch)
sha256sums=('cca3d4a0a5e93ef45b95513a448175e4f9f7bb53bbe560e72e57f0eb37d74397'
            '3c191c0380f991709bd38a0ace6adbf351fca441a2c5fd432e56a0da6b68a728'
            'e4cd7a26d9d0243fe74a52f6391e2eba991625125d5307eee1419ffda6a3cba6'
            'bacfd10d551ff06d9829cecb3e640b352b260c3065a229927271f9a8b16d6316')
prepare() {
  cd "$srcdir"/$pkgname-$_realver
  patch -p1 < $startdir/zero-button.patch
  patch -p2 < $startdir/qbutton-group-inc.patch
}

build() {
  cd "$srcdir"/$pkgname-$_realver
  cmake \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	.
  make
}

package() {
  cd "$srcdir"/$pkgname-$_realver
  make DESTDIR="$pkgdir" install
  install -Dm0644 "resources/appicon.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/scantailor.svg"
  install -Dm0644 "$srcdir/scantailor.desktop" "$pkgdir/usr/share/applications/scantailor.desktop"
}
